dnl Process this file with autoconf to produce a configure script.

dnl Copyright (C) 1994-1997 Sverre Hvammen Johansen,
dnl Department of Informatics, University of Oslo.
dnl
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; version 2.
dnl
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 675 Mass Ave, Cambridge, MA 02139, USA. */

AC_REVISION($Revision: 1.16 $)dnl
AC_INIT(src/cimcomp.c)

AC_CANONICAL_SYSTEM
changequote(,)dnl
target_os_x_version=`echo $target_os|sed -e 's/\([^0-9.]*\)[0-9.]*/\1/'`
changequote([,])dnl
AC_DEFINE_UNQUOTED(SYSTEM_TYPE,"$target")
AC_DEFINE_UNQUOTED(CPU_TYPE,"`echo $target_cpu | tr a-z A-Z`")
AC_DEFINE_UNQUOTED(OS_TYPE_VERSION,"`echo $target_os | tr a-z A-Z`")
AC_DEFINE_UNQUOTED(MANUFACTURER,"`echo $target_vendor | tr a-z A-Z`")
AC_DEFINE_UNQUOTED(OS_TYPE,"`echo $target_os_x_version | tr a-z A-Z`")

AM_INIT_AUTOMAKE(cim, 3.24)
AM_CONFIG_HEADER(config.h)
AM_CYGWIN32
AM_PROG_LIBTOOL

dnl Library version
dnl
dnl If any routines have been removed increment CURRENT and
dnl set REVISION and AGE to 0 (C:R:A becomes C+1:0:0).
dnl
dnl Else if any routines in the library have been added increment
dnl CURRENT and AGE and set REVISION to 0 (C:R:A becomes C+1:0:A+1).
dnl
dnl Else if any changes in the library increment REVISION and
dnl leave CURRENT and AGE unchanged (C:R:A becomes C:R+1:A)
dnl
dnl Else leave CURRENT, REVISION and AGE unchanged.

LIB_CURRENT=3
LIB_REVISION=0
LIB_AGE=0
AC_SUBST(LIB_CURRENT)
AC_SUBST(LIB_REVISION)
AC_SUBST(LIB_AGE)

PACKAGE_VERSION="$PACKAGE-$VERSION"
AC_DEFINE_UNQUOTED(PACKAGE_VERSION, "$PACKAGE_VERSION")
AC_SUBST(PACKAGE_VERSION)

AC_PROG_CC

dnl Checks for programs.
AC_PROG_LN_S
dnl  Not needed when using libtool: AC_PROG_RANLIB
AC_PROG_YACC
AC_PATH_PROG(PERL, perl)

dnl Checks for libraries.
AC_CHECK_LIB(m,main)
AC_CHECK_LIB(ft,main)
AC_HEADER_STDC
AC_CHECK_HEADERS(string.h memory.h malloc.h limits.h values.h fcntl.h sys/resource.h sys/types.h sys/times.h sys/time.h sys/utsname.h signal.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_TIME
AC_STRUCT_TM

dnl Checks for library functions.
AC_CHECK_FUNCS(time times getrusage gettimeofday getdomainname uname gethostname getuid getpid getegid unlink)
AC_FUNC_ALLOCA

AC_C_CHAR_UNSIGNED
AC_CHECK_SIZEOF(int, 4)
AC_CHECK_SIZEOF(long, 4)

AC_C_BIGENDIAN
AC_TYPE_SIGNAL
CIM_TRAP
CIM_BINARY_FILE
CIM_DEFAULTS
CIM_WL_FLAG
CIM_LINK_STATIC_FLAG
CIM_PIC_FLAG

AC_OUTPUT(Makefile cim.spec src/Makefile lib/Makefile test/Makefile man/Makefile doc/Makefile util/Makefile util/cim2latex util/cim2ps util/cimindent, [test -z "$CONFIG_HEADERS" || echo timestamp > stamp-h])
